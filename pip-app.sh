#!/usr/bin/env zsh

export PIPAPP_DIR="$HOME/.pip-apps"
export PATH="$PIPAPP_DIR/bin:$PATH"

pip-app () {

    local name="$1"
    shift

    local venv_dir="$PIPAPP_DIR/virtualenvs/$name"

    if [[ -d "$venv_dir" ]]; then
        echo 'A virtual env with that name already exists.' >&2
        return 1
    fi

    # Create a new virtualenv
    virtualenv --distribute "$@" "$venv_dir"

    # Executables present before installation.
    local execs_before="$(ls "$venv_dir/bin")"

    # Install the app.
    (source "$venv_dir/bin/activate" &&
        pip install "$name")

    # Executables present after installation.
    local execs_after="$(ls "$venv_dir/bin")"

    # The new execs created by the app.
    local execs_new="$(comm -13 <(echo "$execs_before") <(echo "$execs_after"))"

    # Make sure the bin directory exists.
    mkdir -p "$PIPAPP_DIR/bin"

    echo "$execs_new" |
        while read line; do
            {
                echo '#!/usr/bin/env zsh'
                echo "# Generated by pip-app. For the app '$name'."
                echo "source '$venv_dir/bin/activate'"
                echo "exec '$venv_dir/bin/$line'"
            } > "$PIPAPP_DIR/bin/$line"
            chmod +x "$PIPAPP_DIR/bin/$line"
        done

    # Save a record of the executables by this app.
    mkdir -p "$PIPAPP_DIR/manifest"
    echo "$execs_new" |
        sed "s:^:$PIPAPP_DIR/bin/:" > "$PIPAPP_DIR/manifest/$name"

    # Finished.
    echo '--------------------------------------------------'
    echo "Finished installing $name. It added the following executables."
    echo "$(echo "$execs_new" | sed 's/^/\t/')"

}

pip-unapp () {

    local name="$1"

    if [[ -d "$PIPAPP_DIR/virtualenvs/$name" ]]; then
        cat "$PIPAPP_DIR/manifest/$name" | xargs rm
        rm -rf "$PIPAPP_DIR/virtualenvs/$name" \
            "$PIPAPP_DIR/manifest/$name"

        echo "Removed app '$name'."

    else
        echo 'No such app is available.' >&2
        return 1

    fi

}
